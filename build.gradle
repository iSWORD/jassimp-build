buildscript {
	repositories {
        maven {
            url  "http://dl.bintray.com/ihmcrobotics/maven-release"

        }
    }
}

plugins {
   id 'us.ihmc.gradle.ihmc-build' version '0.0.9'
}


apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.6
targetCompatibility = 1.6
version = '4.0.0-ihmc1'
buildDir = 'build-java'


project.ext.fullVersion = version
project.ext.vcsUrl = "https://github.com/assimp/assimp"
project.ext.licenseURL = "https://github.com/assimp/assimp/blob/master/LICENSE"
project.ext.licenseName = "BSD 3-Clause"
project.ext.bintrayLicenseName = "BSD 3-Clause"


sourceSets {
	main {
		java {
			srcDirs += ['assimp/port/jassimp/jassimp/src/']
		}
	}
}

repositories {
  jcenter()
  mavenCentral()
  maven {
      url  "http://dl.bintray.com/ihmcrobotics/maven-release"
  }
}

dependencies {
    compile group: 'us.ihmc', name: 'IHMCNativeLibraryLoader', version: '1.0'
}



task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

jar {
   manifest {
      attributes(
      "Created-By": "IHMC Gradle Build Script",
      "Implementation-Title": project.name,
      "Implementation-Version": project.version,
      "Implementation-Vendor": "IHMC",

      "Bundle-Name": project.name,
      "Bundle-Version": project.version,
      "Bundle-License": "http://www.apache.org/licenses/LICENSE-2.0.txt",
      "Bundle-Vendor": "IHMC")
   }
}

publishing {
	publications {
		mavenJava(MavenPublication) {

			groupId 'us.ihmc.thirdparty'
			artifactId project.name
			version "$version"
			from components.java

			pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.name
                    url project.ext.vcsUrl
                    licenses {
                        license {
                            name project.ext.licenseName
                            url project.ext.licenseURL
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'jsmith'
                            name 'Jesper Smith'
                            email 'jsmith@ihmc.us'
                        }
                    }
                }

        	}

			artifact sourceJar {
				classifier "sources"
			}
		}
	}
}

def labels = ['ihmc', 'robotics', 'java', 'assets', 'models', '3d', 'thidrparty']
ihmc.configureForIHMCOpenSourceBintrayPublish(false, "mavenJava", "maven-release", labels)
